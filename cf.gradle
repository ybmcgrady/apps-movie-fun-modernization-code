def services = [
    [type: "p-mysql", plan: "100mb", instance: "movie-database"],
    [type: "p-mysql", plan: "100mb", instance: "album-database"],
    [type: "p-identity", plan: "p-identity", instance: "movie-fun-sso"],
    [type: "p-service-registry", plan: "standard", instance: "movie-fun-registry"],
    [type: "p-config-server", plan: "standard", instance: "movie-fun-config", config: "config-server.json"],
]

task teardownPcfEnv {
    doLast {

        [
            "cf delete -f album-service",
            "cf delete -f movie-service",
            "cf delete -f movie-fun-app",
        ].each { println it.execute().text }

        services.each {
            println "cf delete-service -f $it.instance".execute().text
        }
    }
}

def createServiceCommand(service) {
    def command = "cf create-service $service.type $service.plan $service.instance"

    if (service.config != null) {
        command += " -c $service.config"
    }

    return command
}

task setupPcfEnv {
    doLast {
        services.each {
            println createServiceCommand(it).execute().text
        }
    }
}

task checkCfServices {
    doLast {
        def serviceNames = services.collect { it.instance }
        def counter = 0
        def allCreated = false
        def timeout = 274_000
        def waitTime = 5_000

        while (!allCreated) {
            if (counter > timeout) {
                throw new Exception("not all CF services have been created after ${timeout / 1000} seconds")
            }

            allCreated = serviceNames.inject(true) { memo, name ->
                def status = "cf service $name".execute().text
                def isCreated = status.contains("create succeeded")

                memo && isCreated
            }

            counter += waitTime
            sleep waitTime
        }
    }
}

task deploy {
    doLast {
        "cf push".execute().in.eachLine { line ->
            println line
        }
    }
}

setupPcfEnv.mustRunAfter teardownPcfEnv
checkCfServices.mustRunAfter setupPcfEnv
deploy.mustRunAfter checkCfServices

task resetPcfEnv(dependsOn: [teardownPcfEnv, setupPcfEnv, checkCfServices, deploy])
